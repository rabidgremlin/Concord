buildscript {    
    dependencies {        
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.3.2'        
    }
}

plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}
apply plugin: 'com.diffplug.gradle.spotless'

project.version = '0.0.1'
mainClassName = 'com.rabidgremlin.concord.ConcordServerApplication'

task utilJar(type: Jar) {
    from(sourceSets.main.output) {
        include "com/rabidgremlin/concord/integration/LabelSuggester"
    }
}

spotless {
    java {
        importOrderFile rootProject.file('buildconf/spotless/concord.importorder')
        eclipseFormatFile rootProject.file('buildconf/spotless/concord-formatting.xml')
        removeUnusedImports()
    }
}

shadowJar {
  mergeServiceFiles()
}

run {
    args = ["server", "src/main/yml/server.yml"]
    main = 'com.rabidgremlin.concord.ConcordServerApplication'
}

task createDatabase(type: JavaExec) {
  classpath sourceSets.main.runtimeClasspath
  args = ["db", "migrate", "src/main/yml/server.yml"]
  main = 'com.rabidgremlin.concord.ConcordServerApplication'
}

task dropDatabase(type: JavaExec) {
  classpath sourceSets.main.runtimeClasspath  
  args = ["db", "drop-all", "--confirm-delete-everything", "src/main/yml/server.yml"]
  main = 'com.rabidgremlin.concord.ConcordServerApplication'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
    }
}

repositories {        
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.rabidgremlin.concord:concord-plugin:0.0.1'
    compile 'io.dropwizard:dropwizard-core:1.3.0'
    compile "io.dropwizard:dropwizard-assets:1.3.0"
    compile "io.dropwizard:dropwizard-auth:1.3.0"
    compile "io.dropwizard:dropwizard-db:1.3.0"
    compile "io.dropwizard:dropwizard-migrations:1.3.0"  
    compile "io.dropwizard:dropwizard-jdbi3:1.3.0"    
    compile('io.swagger:swagger-jersey2-jaxrs:1.5.13') {
      exclude group: 'com.sun.jersey'
    }
    compile 'com.amazonaws:aws-java-sdk-logs:1.11.119'
    compile 'com.github.toastshaman:dropwizard-auth-jwt:1.1.0-0'
    compile 'mysql:mysql-connector-java:6.0.6'
    compile 'io.github.binout:jaxrs-csv:0.9'
    compile 'com.squareup.retrofit2:retrofit:2.4.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.4.0'
    compile 'com.squareup.okhttp3:okhttp:3.12.0'
    compile "com.squareup.okhttp3:logging-interceptor:3.12.0"

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:2.23.4'    
    testCompile 'com.wix:wix-embedded-mysql:4.2.0'
    
}

build.dependsOn shadowJar
build.dependsOn utilJar